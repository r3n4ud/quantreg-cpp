cmake_minimum_required(VERSION 3.19)

set(quantreg-cpp_known_comps static shared)
set(quantreg-cpp_comp_static NO)
set(quantreg-cpp_comp_shared NO)
foreach (quantreg-cpp_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (quantreg-cpp_comp IN_LIST quantreg-cpp_known_comps)
        set(quantreg-cpp_comp_${quantreg-cpp_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "quantreg-cpp does not recognize component `${quantreg-cpp_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (quantreg-cpp_comp_static AND quantreg-cpp_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "quantreg-cpp `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(quantreg-cpp_static_targets "${CMAKE_CURRENT_LIST_DIR}/quantreg-cpp-static-targets.cmake")
set(quantreg-cpp_shared_targets "${CMAKE_CURRENT_LIST_DIR}/quantreg-cpp-shared-targets.cmake")

macro(quantreg_load_targets type)
    if (NOT EXISTS "${quantreg-cpp_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "quantreg-cpp `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${quantreg-cpp_${type}_targets}")
endmacro()

if (quantreg-cpp_comp_static)
    quantreg_load_targets(static)
elseif (quantreg-cpp_comp_shared)
    quantreg_load_targets(shared)
elseif (DEFINED QUANTREG_CPP_SHARED_LIBS AND QUANTREG_CPP_SHARED_LIBS)
    quantreg_load_targets(shared)
elseif (DEFINED QUANTREG_CPP_SHARED_LIBS AND NOT QUANTREG_CPP_SHARED_LIBS)
    quantreg_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${quantreg-cpp_shared_targets}")
        quantreg_load_targets(shared)
    else ()
        quantreg_load_targets(static)
    endif ()
else ()
    if (EXISTS "${quantreg-cpp_static_targets}")
        quantreg_load_targets(static)
    else ()
        quantreg_load_targets(shared)
    endif ()
endif ()
